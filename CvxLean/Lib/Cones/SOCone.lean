import Mathlib.Data.Real.Sqrt
import Mathlib.Data.Matrix.Basic
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import CvxLean.Lib.Math.Data.Real

/-!
We follow the MOSEK modeling cookbook:
https://docs.mosek.com/modeling-cookbook/cqo.html
-/

namespace Real

open BigOperators

variable {n m} [Fintype m] [Fintype n]

/-- The `n`-dimensional second-order cone
      `ğ’¬â¿âºÂ¹ := { (t, x) | â€–xâ€–â‚‚ = sqrt(xâ‚Â² + â‹¯ + xâ‚™Â²) â‰¤ t } âŠ† â„ Ã— â„â¿`. -/
def soCone (t : â„) (x : n â†’ â„) : Prop :=
  sqrt (âˆ‘ i, x i ^ 2) â‰¤ t

/-- The `n`-dimensional rotated second-order cone
      `ğ’¬áµ£â¿âºÂ² := { (v, w, x) | xâ‚Â² + â‹¯ + xâ‚™Â² â‰¤ 2vw âˆ§ 0 â‰¤ v, w } âŠ† â„ Ã— â„ Ã— â„â¿`. -/
def rotatedSoCone (v w : â„) (x : n â†’ â„) : Prop :=
  (âˆ‘ i, x i ^ 2) â‰¤ (v * w) * 2 âˆ§ 0 â‰¤ v âˆ§ 0 â‰¤ w

/-- `m` copies of the `n`-dimensional second-order cone `(ğ’¬â¿)áµ`. -/
def Vec.soCone (t : m â†’ â„) (X : Matrix m n â„) : Prop :=
  âˆ€ i, Real.soCone (t i) (X i)

/-- `m` copies of the `n`-dimensional rotated second-order cone `(ğ’¬áµ£â¿)áµ`. -/
def Vec.rotatedSoCone (v w : m â†’ â„) (X : Matrix m n â„) : Prop :=
  âˆ€ i, Real.rotatedSoCone (v i) (w i) (X i)

noncomputable section ConeConversion

/-- If `(t, x) âˆˆ ğ’¬â¿âºÂ¹` then `r(t, x) âˆˆ ğ’¬áµ£â¿âºÂ²`. -/
def rotateSoCone {n : â„•} (t : â„) (x : Fin n.succ â†’ â„) : â„ Ã— â„ Ã— (Fin n â†’ â„) :=
  ((t + x 0) / sqrt 2, (t - x 0) / sqrt 2, fun i => x i.succ)

lemma rotateSoCone_rotatedSoCone {n : â„•} {t : â„} {x : Fin n.succ â†’ â„}
  (h : soCone t x) :
  let (v, w, x) := rotateSoCone t x; rotatedSoCone v w x := by
  simp [rotatedSoCone, rotateSoCone]
  have habsx0t : |x 0| â‰¤ t := by
    rw [soCone, Fin.sum_univ_succ] at h
    have hS : 0 â‰¤ âˆ‘ i : Fin n, x (Fin.succ i) ^ 2 :=
      Finset.sum_nonneg (fun _ _ => sq_nonneg _)
    exact abs_le_of_sqrt_sq_add_nonneg_le hS h
  have ht : 0 â‰¤ t := le_trans (abs_nonneg _) habsx0t
  replace âŸ¨hx0t, hnx0tâŸ© := abs_le.mp habsx0t
  split_ands
  { field_simp
    have hrw : (t + x 0) * (t - x 0) = t ^ 2 - x 0 ^ 2 := by ring
    rw [hrw, le_sub_iff_add_le, add_comm]
    rw [â†Fin.sum_univ_succ (f := fun i => (x i) ^ 2)]
    rw [â†sqrt_le_left ht]
    exact h }
  { simp [le_div_iff]; linarith }
  { simp [le_div_iff]; linarith }

/-- If `(v, w, x) âˆˆ ğ’¬â¿âºÂ²` then `u(v, w, x) âˆˆ ğ’¬áµ£â¿âºÂ¹`. -/
def unrotateSoCone {n : â„•} (v w : Real) (x : Fin n â†’ â„) :
  â„ Ã— (Fin n.succ â†’ â„) :=
  ((v + w) / sqrt 2, Matrix.vecCons ((v - w) / sqrt 2) x)

lemma unrotateSoCone_soCone {n : â„•} {v w : â„} {x : Fin n â†’ â„}
  (h : rotatedSoCone v w x) :
  let (t, x) := unrotateSoCone v w x; soCone t x := by
  simp [soCone, unrotateSoCone]
  replace âŸ¨h, hv, hwâŸ© := h
  rw [sqrt_le_iff]
  split_ands
  { simp [le_div_iff]; linarith }
  { rw [Fin.sum_univ_succ]
    simp [Matrix.vecCons]
    rw [add_comm, â†le_sub_iff_add_le]
    field_simp
    have hrw : ((v + w) ^ 2 - (v - w) ^ 2) / 2 = v * w * 2 := by ring
    rwa [hrw] }

-- TODO(RFM): rotate then unrotate?
-- TODO(RFM): unrotate then rotate?

end ConeConversion

section Lemmas

/-- To handle powers, a common trick is to use the fact that for
`x, y â‰¥ 0` and `z âˆˆ â„`,
      `((x + y), (x - y, 2z)áµ€) âˆˆ ğ’¬â¿âºÂ¹ â†” z ^ 2 â‰¤ xy`. -/
lemma soCone_add_sub_two_mul_of_nonneg {x y : â„} (z : â„)
  (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) :
  soCone (x + y) ![x - y, 2 * z] â†” z ^ (2 : â„) â‰¤ x * y := by
  have hxy := add_nonneg hx hy
  conv => lhs; unfold soCone; simp [sqrt_le_left hxy, â†le_sub_iff_add_le']
  ring_nf; simp

/-- Same as `soCone_add_sub_two_mul_of_nonneg` with `z = 1`. -/
lemma soCone_add_sub_two_of_nonneg {x y : â„} (hx : 0 â‰¤ x) (hy : 0 â‰¤ y) :
  soCone (x + y) ![x - y, 2] â†” 1 â‰¤ x * y := by
  have h := soCone_add_sub_two_mul_of_nonneg 1 hx hy
  rw [mul_one, one_rpow] at h
  exact h

/-- Same as `soCone_add_sub_two_mul_of_nonneg` replacing `y` by `-y`. -/
lemma soCone_sub_add_two_mul_of_nonneg {x y : â„} (z : â„) :
  soCone (x - y) ![x + y, 2 * z] â†” y â‰¤ x âˆ§ z ^ (2 : â„) â‰¤ -(x * y) := by
  conv => lhs; unfold soCone; simp [sqrt_le_iff, â†le_sub_iff_add_le']
  apply Iff.and
  { rfl }
  { ring_nf!; rw [â†neg_mul, â†div_le_iff (by norm_num)]; simp }

end Lemmas

end Real
