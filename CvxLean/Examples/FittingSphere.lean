import CvxLean

/-!
# Case study: Fitting a sphere to data

See exercise 8.16 in https://github.com/cvxgrp/cvxbook_additional_exercises.
-/

noncomputable section

open CvxLean Minimization Real BigOperators Matrix Finset

section LeastSquares

def leastSquares {n : ‚Ñï} (a : Fin n ‚Üí ‚Ñù) :=
  optimization (x : ‚Ñù)
    minimize (‚àë i, ((a i - x) ^ 2) : ‚Ñù)

@[reducible]
def mean {n : ‚Ñï} (a : Fin n ‚Üí ‚Ñù) : ‚Ñù := (1 / n) * ‚àë i, (a i)

/-- It is useful to rewrite the sum of squares in the following way to prove
`leastSquares_optimal_eq_mean`, following Marty Cohen's answer in
https://math.stackexchange.com/questions/2554243. -/
lemma leastSquares_alt_objFun {n : ‚Ñï} (hn : 0 < n) (a : Fin n ‚Üí ‚Ñù) (x : ‚Ñù) :
  (‚àë i, ((a i - x) ^ 2)) = n * ((x - mean a) ^ 2 + (mean (a ^ 2) - (mean a) ^ 2)) := by
  calc
  -- 1) Œ£ (a·µ¢ - x)¬≤ = Œ£ (a·µ¢¬≤ - 2a·µ¢x + x¬≤)
  _ = ‚àë i, ((a i) ^ 2 - 2 * (a i) * x + (x ^ 2)) := by
    congr; funext i; simp; ring
  -- 2) ... = Œ£ a·µ¢¬≤ - 2xŒ£ a·µ¢ + nx¬≤
  _ = ‚àë i, ((a i) ^ 2) - 2 * x * ‚àë i, (a i) + n * (x ^ 2) := by
    rw [sum_add_distrib, sum_sub_distrib, ‚Üê sum_mul, ‚Üê mul_sum]; simp [sum_const]; ring
  -- 3) ... = n{a¬≤} - 2xn{a} + nx¬≤
  _ = n * mean (a ^ 2) - 2 * x * n * mean a + n * (x ^ 2) := by
    simp [mean]; field_simp; ring
  -- 4) ... = n((x - {a})¬≤ + ({a¬≤} - {a}¬≤))
  _ = n * ((x - mean a) ^ 2 + (mean (a ^ 2) - (mean a) ^ 2)) := by
    simp [mean]; field_simp; ring

/-- Key result about least squares: `x* = mean a`. -/
lemma leastSquares_optimal_eq_mean {n : ‚Ñï} (hn : 0 < n) (a : Fin n ‚Üí ‚Ñù) (x : ‚Ñù)
  (h : (leastSquares a).optimal x) : x = mean a := by
  simp [optimal, feasible, leastSquares] at h
  replace h : ‚àÄ y, (x - mean a) ^ 2 ‚â§ (y - mean a) ^ 2  := by
    intros y
    have hy := h y
    have h_rw_x := leastSquares_alt_objFun hn a x
    have h_rw_y := leastSquares_alt_objFun hn a y
    simp only [rpow_two] at h_rw_x h_rw_y ‚ä¢
    rwa [h_rw_x, h_rw_y, mul_le_mul_left (by positivity), add_le_add_iff_right] at hy
  have hmean := h (mean a)
  simp at hmean
  have h_sq_eq_zero := le_antisymm hmean (sq_nonneg _)
  rwa [sq_eq_zero_iff, sub_eq_zero] at h_sq_eq_zero

def leastSquaresVec {n : ‚Ñï} (a : Fin n ‚Üí ‚Ñù) :=
  optimization (x : ‚Ñù)
    minimize (Vec.sum ((a - Vec.const n x) ^ 2) : ‚Ñù)

/-- Same as `leastSquares_optimal_eq_mean` in vector notation. -/
lemma leastSquaresVec_optimal_eq_mean {n : ‚Ñï} (hn : 0 < n) (a : Fin n ‚Üí ‚Ñù) (x : ‚Ñù)
  (h : (leastSquaresVec a).optimal x) : x = mean a := by
  apply leastSquares_optimal_eq_mean hn a
  simp [leastSquaresVec, leastSquares, optimal, feasible] at h ‚ä¢
  intros y
  simp only [Vec.sum, Pi.pow_apply, Pi.sub_apply, Vec.const, rpow_two] at h
  exact h y

end LeastSquares

namespace FittingSphere

-- Dimension.
variable (n : ‚Ñï)

-- Number of points.
variable (m : ‚Ñï)

-- Given points.
variable (x : Fin m ‚Üí Fin n ‚Üí ‚Ñù)

def fittingSphere :=
  optimization (c : Fin n ‚Üí ‚Ñù) (r : ‚Ñù)
    minimize (‚àë i, (‚Äñ(x i) - c‚Äñ ^ 2 - r ^ 2) ^ 2 : ‚Ñù)
    subject to
      h‚ÇÅ : 0 < r

instance : ChangeOfVariables fun ((c, t) : (Fin n ‚Üí ‚Ñù) √ó ‚Ñù) => (c, sqrt (t + ‚Äñc‚Äñ ^ 2)) :=
  { inv := fun (c, r) => (c, r ^ 2 - ‚Äñc‚Äñ ^ 2),
    condition := fun (_, r) => 0 ‚â§ r,
    property := fun ‚ü®c, r‚ü© h => by simp [sqrt_sq h] }

equivalence' eqv/fittingSphereT (n m : ‚Ñï) (x : Fin m ‚Üí Fin n ‚Üí ‚Ñù) : fittingSphere n m x := by
  -- Change of variables.
  equivalence_step =>
    apply ChangeOfVariables.toEquivalence
      (fun (ct : (Fin n ‚Üí ‚Ñù) √ó ‚Ñù) => (ct.1, sqrt (ct.2 + ‚Äñct.1‚Äñ ^ 2)))
    . rintro _ h; exact le_of_lt h
  rename_vars [c, t]
  -- Clean up.
  conv_constr h‚ÇÅ => dsimp
  conv_obj => dsimp
  -- Rewrite objective.
  equivalence_step =>
    apply Equivalence.rewrite_objFun
      (g := fun (ct : (Fin n ‚Üí ‚Ñù) √ó ‚Ñù) =>
        Vec.sum (((Vec.norm x) ^ 2 - 2 * (Matrix.mulVec x ct.1) - Vec.const m ct.2) ^ 2))
    . rintro ‚ü®c, t‚ü© h
      dsimp at h ‚ä¢; simp [Vec.sum, Vec.norm, Vec.const]; congr; funext i; congr 1;
      rw [norm_sub_sq (ùïú := ‚Ñù) (E := Fin n ‚Üí ‚Ñù), sq_sqrt (rpow_two _ ‚ñ∏ le_of_lt (sqrt_pos.mp h))]
      simp [mulVec, inner, dotProduct]
  rename_vars [c, t]

#print fittingSphereT
-- optimization (c : Fin n ‚Üí ‚Ñù) (t : ‚Ñù)
--   minimize Vec.sum ((Vec.norm x ^ 2 - 2 * mulVec x c - Vec.const m t) ^ 2)
--   subject to
--     h‚ÇÅ : 0 < sqrt (t + ‚Äñc‚Äñ ^ 2)

-- Next, we proceed to remove the non-convex constraint by arguing that any (non-trivial) point that
-- minimizes the objective function without the constraint, also satisfies the constraint. We define
-- the problem directly, but note that we could also remove the constraint using the `relaxation`
-- command.

def fittingSphereConvex (n m : ‚Ñï) (x : Fin m ‚Üí Fin n ‚Üí ‚Ñù) :=
  optimization (c : Fin n ‚Üí ‚Ñù) (t : ‚Ñù)
    minimize (Vec.sum ((Vec.norm x ^ 2 - 2 * mulVec x c - Vec.const m t) ^ 2) : ‚Ñù)

/-- If the squared error is zero, then `a·µ¢ = x`. -/
lemma vec_squared_norm_error_eq_zero_iff {n m : ‚Ñï} (a : Fin m ‚Üí Fin n ‚Üí ‚Ñù) (x : Fin n ‚Üí ‚Ñù) :
    ‚àë i, ‚Äña i - x‚Äñ ^ 2 = 0 ‚Üî ‚àÄ i, a i = x := by
  simp [rpow_two]
  rw [sum_eq_zero_iff_of_nonneg (fun _ _ => sq_nonneg _)]
  constructor
  . intros h i
    have hi := h i (by simp)
    rwa [sq_eq_zero_iff, norm_eq_zero, sub_eq_zero] at hi
  . intros h i _
    rw [sq_eq_zero_iff, norm_eq_zero, sub_eq_zero]
    exact h i

/-- This tells us that solving the relaxed problem is sufficient for optimal points if the solution
is non-trivial. -/
lemma optimal_convex_implies_optimal_t (hm : 0 < m) (c : Fin n ‚Üí ‚Ñù) (t : ‚Ñù)
  (h_nontrivial : x ‚â† Vec.const m c)
  (h_opt : (fittingSphereConvex n m x).optimal (c, t)) : (fittingSphereT n m x).optimal (c, t) := by
  simp [fittingSphereT, fittingSphereConvex, optimal, feasible] at h_opt ‚ä¢
  constructor
  . let a := Vec.norm x ^ 2 - 2 * mulVec x c
    have h_ls : optimal (leastSquaresVec a) t := by
      refine ‚ü®trivial, ?_‚ü©
      intros y _
      simp [objFun, leastSquaresVec]
      exact h_opt c y
    -- Apply key result about least squares to `a` and `t`.
    have h_t_eq := leastSquaresVec_optimal_eq_mean hm a t h_ls
    have h_c2_eq : ‚Äñc‚Äñ ^ 2 = (1 / m) * ‚àë i : Fin m, ‚Äñc‚Äñ ^ 2 := by
      simp [sum_const]
      field_simp; ring
    have h_t_add_c2_eq : t + ‚Äñc‚Äñ ^ 2 = (1 / m) * ‚àë i, ‚Äñ(x i) - c‚Äñ ^ 2 := by
      rw [h_t_eq]; dsimp [mean]
      rw [h_c2_eq, mul_sum, mul_sum, mul_sum, ‚Üê sum_add_distrib]
      congr; funext i; rw [‚Üê mul_add]
      congr; simp [Vec.norm]
      rw [norm_sub_sq (ùïú := ‚Ñù) (E := Fin n ‚Üí ‚Ñù)]
      congr
    -- We use the result to establish that `t + ‚Äñc‚Äñ ^ 2` is non-negative.
    have h_t_add_c2_nonneg : 0 ‚â§ t + ‚Äñc‚Äñ ^ 2 := by
      rw [h_t_add_c2_eq]
      apply mul_nonneg (by norm_num)
      apply sum_nonneg
      intros i _
      rw [rpow_two]
      exact sq_nonneg _
    cases (lt_or_eq_of_le h_t_add_c2_nonneg) with
    | inl h_t_add_c2_lt_zero =>
        -- If it is positive, we are done.
        convert h_t_add_c2_lt_zero; simp
    | inr h_t_add_c2_eq_zero =>
        -- Otherwise, it contradicts the non-triviality assumption.
        exfalso
        rw [h_t_add_c2_eq, zero_eq_mul] at h_t_add_c2_eq_zero
        rcases h_t_add_c2_eq_zero with (hc | h_sum_eq_zero)
        . simp at hc; linarith
        rw [vec_squared_norm_error_eq_zero_iff] at h_sum_eq_zero
        apply h_nontrivial
        funext i
        exact h_sum_eq_zero i
  . intros c' x' _
    exact h_opt c' x'

/-- We express the nontriviality condition only in terms of `x` so that it can be checked. -/
lemma non_triviality_condition (c : Fin n ‚Üí ‚Ñù) (hx : ‚àÉ i j, x i ‚â† x j) : x ‚â† Vec.const m c := by
  intros h
  conv at hx => congr; ext i; rw [‚Üê not_forall]
  rw [‚Üê not_forall] at hx
  apply hx
  intros i j
  rw [congr_fun h i, congr_fun h j]
  simp [Vec.const]

/-- We show that we have a reduction via the identity map. -/
def red (hm : 0 < m) (hx : ‚àÉ i j, x i ‚â† x j) :
    (fittingSphereT n m x) ‚âº (fittingSphereConvex n m x) :=
  { psi := id,
    psi_optimality := fun (c, t) h_opt => by
      have h_nontrivial := non_triviality_condition n m x c hx
      exact optimal_convex_implies_optimal_t n m x hm c t h_nontrivial h_opt }

#print fittingSphereConvex
-- optimization (c : Fin n ‚Üí ‚Ñù) (t : ‚Ñù)
--   minimize Vec.sum ((Vec.norm x ^ 2 - 2 * mulVec x c - Vec.const m t) ^ 2)

-- We proceed to solve the problem on a concrete example.
-- https://github.com/cvxgrp/cvxbook_additional_exercises/blob/main/python/sphere_fit_data.py

@[optimization_param]
def n‚Çö := 2

@[optimization_param]
def m‚Çö := 10

@[optimization_param]
def x‚Çö : Fin m‚Çö ‚Üí Fin n‚Çö ‚Üí ‚Ñù := Matrix.transpose <| ![
  ![1.824183228637652032e+00, 1.349093690455489103e+00, 6.966316403935147727e-01,
    7.599387854623529392e-01, 2.388321695850912363e+00, 8.651370608981923116e-01,
    1.863922545015865406e+00, 7.099743941474848663e-01, 6.005484882320809570e-01,
    4.561429569892232472e-01],
  ![-9.644136284187876385e-01, 1.069547315003422927e+00, 6.733229334437943470e-01,
    7.788072961810316164e-01, -9.467465278344706636e-01, -8.591303443863639311e-01,
    1.279527420871080956e+00, 5.314829019311283487e-01, 6.975676079749143499e-02,
    -4.641873429414754559e-01]]

-- We use the `solve` command on the data above.

solve fittingSphereConvex n‚Çö m‚Çö x‚Çö

-- Finally, we recover the solution to the original problem.

def sol := eqv.backward_map n‚Çö m‚Çö x‚Çö.float fittingSphereConvex.solution

def c‚Çö_opt := sol.1
def r‚Çö_opt := sol.2

#eval c‚Çö_opt -- ![1.664863, 0.031932]
#eval r‚Çö_opt -- 1.159033

end FittingSphere

end
