/-!
# Representation of numerical problem data

This file defines an intermediate representation of a problem in conic form at the level of floats.
We provide an interface to build this data by adding any of the possible types of constraints. This
data can then be transformed in a straightforward manner into Conic Benchmark Format, for example.

The procedure that creates `ProblemData` from an optimization problem can be found in
`CvxLean/Command/Solve/Coeffs.lean`.
-/

namespace CvxLean

/-- Cones admitting scalar affine constraints. Note that the only cone that admits matrix affine
constraints is the PSD cone. -/
inductive ScalarConeType
  | Zero | PosOrth | Exp | Q | QR

namespace ScalarConeType

instance : ToString ScalarConeType where
  toString
  | Zero => "Zero"
  | PosOrth => "PosOrth"
  | Exp => "Exp"
  | Q => "Q"
  | QR => "QR"

end ScalarConeType

/-- Encodes the expression `âŸ¨X, AâŸ© + âˆ‘ i, aáµ¢ xáµ¢ + b`. -/
structure ScalarAffine :=
  (n m : Nat)
  (A : Array (Array Float))
  (a : Array Float)
  (b : Float)

namespace ScalarAffine

instance : ToString ScalarAffine where
  toString sa := s!"ScalarAffine [{sa.n}, {sa.m}, {sa.A}, {sa.a}, {sa.b}]"

end ScalarAffine

/-- Encodies the expression `âˆ‘ i, xáµ¢ â€¢ Háµ¢ + D`. -/
structure MatrixAffine :=
  (n : Nat)
  (H : Array (Array (Array Float)))
  (D : Array (Array Float))

namespace MatrixAffine

instance : ToString MatrixAffine where
  toString sa := s!"MatrixAffine [{sa.n}, {sa.H}, {sa.D}]"

end MatrixAffine

/-- Data structure storing the floating-point coefficient of the objective function and constraints
of a problem in conic form. -/
structure ProblemData :=
  (objective : Option ScalarAffine)
  (scalarAffineConstraints : Array (ScalarAffine Ã— ScalarConeType))
  (matrixAffineConstraints : Array MatrixAffine)

namespace ProblemData

instance : ToString ProblemData where
  toString data :=
    let scalarConstraintsStr := data.scalarAffineConstraints.map
      (fun (sa, sct) => (toString sct) ++ " " ++ (toString sa))
    let matrixConstraintsStr := data.matrixAffineConstraints.map
      (fun ma => "PSD " ++ (toString ma))
    let constraintsStr := (scalarConstraintsStr ++ matrixConstraintsStr).map
      (fun s => "* " ++ s ++ "\n")
    "Objective: " ++ toString (data.objective) ++ "\n" ++
    "Constraints: \n" ++ String.join constraintsStr.data

def empty : ProblemData :=
  ProblemData.mk none #[] #[]

instance : Inhabited ProblemData where
  default := empty

/-- Set full objective function of the form `âŸ¨X, AâŸ© + âˆ‘ i, aáµ¢ xáµ¢ + b`. -/
def setObjective (data : ProblemData) (A : Array (Array Float)) (a : Array Float) (b : Float) :
    ProblemData :=
  { data with objective := ScalarAffine.mk A.size a.size A a b }

/-- Same as `setObjective` if the objective is of the form `âˆ‘ i, aáµ¢xáµ¢ + b`. -/
def setObjectiveOnlyVector (data : ProblemData) (a : Array Float) (b : Float) : ProblemData :=
  data.setObjective #[] a b

/-- Same as `setObjective` if the objective is of the form `âŸ¨A, XâŸ© + b`. -/
def setObjectiveOnlyMatrix (data : ProblemData) (A : Array (Array Float)) (b : Float) :
    ProblemData :=
  data.setObjective A #[] b

/-- Add a scalar affine constraint of the form ``âŸ¨X, AâŸ© + âˆ‘ i, aáµ¢ xáµ¢ + b âˆˆ ğ’¦`, where `ğ’¦` is a cone
of type `sct`. -/
def addScalarAffineConstraint (data : ProblemData) (A : Array (Array Float)) (a : Array Float)
    (b : Float) (sct : ScalarConeType) : ProblemData :=
  let constraint := ScalarAffine.mk A.size a.size A a b
  { data with scalarAffineConstraints :=
      data.scalarAffineConstraints.push âŸ¨constraint, sctâŸ© }

/-- Same as `addScalarAffineConstraint` if the constraint is of the form `âˆ‘ i, aáµ¢ xáµ¢ + b âˆˆ ğ’¦`. -/
def addScalarAffineConstraintOnlyVector (data : ProblemData) (a : Array Float) (b : Float)
    (sct : ScalarConeType) : ProblemData :=
  data.addScalarAffineConstraint #[] a b sct

/-- Add zero cone constraint `âˆ‘ i, aáµ¢xáµ¢ + b âˆˆ 0` to problem data. -/
def addZeroConstraint (data : ProblemData) (a : Array Float) (b : Float) : ProblemData :=
  data.addScalarAffineConstraintOnlyVector a b ScalarConeType.Zero

/-- Add exponential cone constraint `âˆ‘ i, aáµ¢xáµ¢ + b âˆˆ ğ’¦â‚‘` to problem data. Note that the
second-order cone is `3`-dimensional, so to capture `(x, y, z) âˆˆ ğ’¦â‚‘` we do `x âˆˆ ğ’¦â‚‘`, `y âˆˆ ğ’¦â‚‘`, and
`z âˆˆ ğ’¦â‚‘` consecutively. We keep track of how to group consecutive constraints in
`CvxLean/Command/Solve/Float/Coeffs.lean`. -/
def addExpConstraint (data : ProblemData) (a : Array Float) (b : Float) : ProblemData :=
  data.addScalarAffineConstraintOnlyVector a b ScalarConeType.Exp

/-- Add positive orthant cone constraint `âˆ‘ i, aáµ¢xáµ¢ + b âˆˆ â„â‚Š` to problem data. -/
def addPosOrthConstraint (data : ProblemData) (a : Array Float) (b : Float) : ProblemData :=
  data.addScalarAffineConstraintOnlyVector a b ScalarConeType.PosOrth

/-- Add second-order cone constraint `âˆ‘ i, aáµ¢xáµ¢ + b âˆˆ ğ’¬` to problem data. Note that the second-order
cone is `n+1`-dimensional. The same remark on grouping constraints in `addExpConstraint` applies. -/
def addSOConstraint (data : ProblemData) (a : Array Float) (b : Float) : ProblemData :=
  data.addScalarAffineConstraintOnlyVector a b ScalarConeType.Q

/- Add second-order cone constraint `âˆ‘ i, aáµ¢xáµ¢ + b âˆˆ ğ’¬áµ£` to problem data. Note that the rotated
second-order cone is `n+2`-dimensional. The same remark on grouping constraints in
`addExpConstraint` applies. -/
def addRotatedSOConstraint (data : ProblemData) (a : Array Float) (b : Float) : ProblemData :=
  data.addScalarAffineConstraintOnlyVector a b ScalarConeType.QR

/-- Add a matrix affine constraint `âˆ‘ i, xáµ¢ â€¢ Háµ¢ + D âˆˆ ğ’®â‚Šâ¿` to problem data. The only matrix cone
we consider is the PSD cone. -/
def addMatrixAffineConstraint (data : ProblemData) (H : Array (Array (Array Float)))
    (D : Array (Array Float)) : ProblemData :=
  let constraint := MatrixAffine.mk D.size H D
  { data with matrixAffineConstraints := data.matrixAffineConstraints.push constraint }

end ProblemData

/-- Indices to group constraints together and tag cones with the correct dimension when translating
problem data to solver formats. -/
def ScalarAffineSections : Type := Array Nat

end CvxLean
